(ns mail.message-test
  (:require [clojure.test :refer [deftest is]]
            [mail.draft :as draft]
            [mail.message :as msg]
            [mail.receipt :as receipt]))

(deftest validates-message-shape
  (let [m (msg/message {:from "Ops@Example.COM"
                        :to ["alice@example.com"]
                        :subject "Hello"
                        :text "Hi"})]
    (is (msg/valid-message? m))
    (is (= "ops@example.com" (get-in m [:mail/from :mail.address/email])))
    (is (= ["alice@example.com"]
           (mapv :mail.address/email (msg/recipients m))))))

(deftest rejects-invalid-message
  (let [m (msg/message {:from "bad"
                        :to []
                        :subject ""
                        :text nil})]
    (is (= #{:invalid-from :missing-to :missing-subject :missing-body}
           (set (map :mail.error/code (msg/validation-errors m)))))
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs js/Error) (msg/assert-valid-message m)))))

(deftest draft-send-effect-requires-approval
  (let [m (msg/message {:from "ops@example.com"
                        :to "alice@example.com"
                        :subject "Deploy"
                        :text "Approved?"})
        d (draft/draft "d1" m)]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs js/Error) (draft/send-effect d)))
    (is (= :mail/send (:mail.effect/type (draft/send-effect (draft/approve d {:by "human"})))))))

(deftest receipt-fact-is-provider-independent
  (let [m (msg/message {:from "ops@example.com"
                        :to "alice@example.com"
                        :subject "Deploy"
                        :text "Done"})
        effect (-> (draft/draft "d2" m)
                   (draft/approve {:by "human"})
                   draft/send-effect)
        r (receipt/receipt effect :resend {:message-id "msg_123"})]
    (is (= :mail/receipt (:kotoba/type (receipt/receipt-fact r))))
    (is (= "msg_123" (:mail.receipt/provider-message-id r)))))
